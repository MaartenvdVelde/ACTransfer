// Model of the Stroop task
// Adapatation of original PR model

define task stroop {
    initial-goals: (stroop neutral)
    goals: (attendcolor default-attend)
    task-constants: (neutral focuscolor)
    start-screen: stroop-start
    lf: 0.2
    egs: 0.05
    rt: -3.0
    alpha: 0.05
    default-activation: 1.0
    input-activation: 1.0
    default-operator-assoc: 8.0
    default-inter-operator-assoc: 2.0
    goal-operator-learning: t
    beta: 0.05
    reward: 10.0
    explore-exploit: 0.0
}

define goal stroop {

    operator retrieve-response {
        V2 <> nil
        RT1 = nil
        ==>
        response -> RT1
        V2 -> RT2
    }

    operator say-color {
        RT3 <> nil
        ==>
        say -> AC1
        RT3 -> AC2
        stop -> G1
    }

}

define goal default-attend {

    operator just-wait(activation=1.5) {
    "Just wait for the stimulus"
        V1 = fixation
        ==>
        wait -> AC1
    }


    operator attend(activation=1.5) {
        V1 = stim
        V2 = nil
        ==>
        attend -> AC1
    }
}

define goal attendcolor {

    operator prepare {
        "Prepare for the upcoming stimulus"
        V1 = fixation
        G2 = neutral
        ==>
        focuscolor -> G2
    }

    operator wait-when-prepared {
        V1 = fixation
        G2 = focuscolor
        ==>
        wait -> AC1
    }

    operator attendjustcolor {
        V1 = stim
        V2 = nil
        G2 = focuscolor
        ==>
        attendcolor -> AC1
    }
}

define facts {
    (red-word-assoc response red-word red :activation 0.0)
    (blue-word-assoc response blue-word blue :activation 0.0)
    (red-color-assoc response red-color red :activation 0.0)
    (blue-color-assoc response blue-color blue :activation 0.0)
}

define sji {
    (red-word red-word-assoc 1.5)(red-word blue-word-assoc -1.5)
    (red-color blue-color-assoc -1.5)(red-color red-color-assoc 1.5)
    (blue-word red-word-assoc -1.5)(blue-word blue-word-assoc 1.5)
    (blue-word red-color-assoc -1.5)(blue-word blue-color-assoc 1.5)
    (red-word blue-color-assoc -1.5)(red-word red-color-assoc 1.5)
    (blue-color blue-color-assoc 1.5)(blue-color red-color-assoc -1.5)
}

define screen stroop-start {
(fixation)
}

define screen stimulus {
(stim)
}

define screen allproperties {
(stim ?2 ?3)
}

define screen colorproperty {
(stim ?2)
}

transition(stroop-start,stimulus) = absolute-time(1.0)
transition(stimulus,allproperties) = action(attend)
transition(stimulus,colorproperty) = action(attendcolor)

define goal-action {
(say ?4)

}

define inputs {
    (consistent1 stim red-color red-word red)
    (consistent2 stim blue-color blue-word blue)
    (conflict1 stim red-color blue-word red)
    (conflict2 stim blue-color red-word blue)
}